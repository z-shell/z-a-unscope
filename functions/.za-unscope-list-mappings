# -*- mode: zsh; sh-indentation: 2; indent-tabs-mode: nil; sh-basic-offset: 2; -*-
# vim: ft=zsh sw=2 ts=2 et
#
# Copyright (c) 2021 Z-Shell Community

.za-unscope-list-mappings() {
  builtin emulate -LR zsh
  builtin setopt extendedglob warncreateglobal typesetsilent noshortloops rcquotes

  local -a keys
  keys=( ${(kon)zi_annex_unscope_mappings[@]} )

  if (( !${#keys} )) {
    +zi-message "{annex}unscope-annex{ehi}:{rst}{msg} No mappings found." \
    "(Missing hash variable{ehi}:" \
    "{rst}{var}\$zi_annex_unscope_mappings{msg}){rst}"
    return 1
  }

  +zi-message "{p}#{rst}     {obj2}${(r:21:: :):-Short Name}{rst}" \
    "{txt}Full (Scoped) Plugin ID{rst}"
  local key
  for key ( $keys ) {
    +zi-message "{meta}${(M)key##<->.[[:space:]]##}" \
    "{data}${(r:17:: :)${key##<->.[[:space:]]##}}" \
    "{ehi}â†’{rst}  " "{msg}${zi_annex_unscope_mappings[$key]% <->}{rst}"
  }
}
